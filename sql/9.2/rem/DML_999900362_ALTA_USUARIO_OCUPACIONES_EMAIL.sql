--/*
--/*
--###########################################
--## AUTOR=Carlos Esquerdo
--## FECHA_CREACION=20181013
--## ARTEFACTO=online
--## VERSION_ARTEFACTO=2.0.18
--## INCIDENCIA_LINK=HREOS-4536
--## PRODUCTO=NO
--## 
--## Finalidad: Creacion de Usuarios REM
--##			
--## INSTRUCCIONES:  
--## VERSIONES:
--##        0.1 Versión inicial
--###########################################
----*/


WHENEVER SQLERROR EXIT SQL.SQLCODE;
SET SERVEROUTPUT ON; 
SET DEFINE OFF;


DECLARE
	V_ESQUEMA VARCHAR2(25 CHAR):= '#ESQUEMA#'; -- Configuracion Esquema
	V_ESQUEMA_M VARCHAR2(25 CHAR):= '#ESQUEMA_MASTER#'; -- Configuracion Esquema Master
	ERR_NUM NUMBER(25);  -- Vble. auxiliar para registrar errores en el script.
	ERR_MSG VARCHAR2(1024 CHAR); -- Vble. auxiliar para registrar errores en el script.
  
	V_CONT NUMBER(16);
	V_ZON_ID NUMBER(16);
	V_PEF_ID NUMBER(16);
	V_USU_ID NUMBER(16);
	V_DES_ID NUMBER(16);
	V_GRU_ID NUMBER(16);
	V_AUX NUMBER(16);
	V_AUX_VAR VARCHAR2(50);
	
	VV_USERNAME VARCHAR2(25);
	VV_NOMBRE VARCHAR2(50);
	VV_APELLIDO1 VARCHAR2(50);
	VV_APELLIDO2 VARCHAR2(50);
	VV_EMAIL VARCHAR2(50);
	VV_PASS VARCHAR2(25);
	VV_PEF VARCHAR2(25);
	VV_ES_GRU NUMBER(16);
    VV_GRU VARCHAR2(25);
	VV_DES VARCHAR2(25);
	VV_TICKET VARCHAR2(25);
	
	USD_GESTOR_DEFECTO NUMBER(16) := 1;
	USD_SUPERVISOR NUMBER(16) := 1;
  
	TYPE T_TIPO_DATA IS TABLE OF VARCHAR2(200);
	TYPE T_ARRAY_DATA IS TABLE OF T_TIPO_DATA;
	V_TIPO_DATA T_ARRAY_DATA := T_ARRAY_DATA(
		--	  USER_NAME	   	NOMBRE_USU		APELL1			APELL2			EMAIL      							PASS		PEF_COD		USU_GRUPO, ES_GRUPO, 	DESPACHO_EXTERNO	TIKET
		T_TIPO_DATA( 'emailOcupaciones' ,'emailOcupaciones' ,''  ,'Ocupaciones'  ,'pruebashrem@gmail.com'  , 'DSWRCQRAYC'  ,'HAYACONSU'  ,''  ,0 ,'' ,'HREOS-4536')
	); 
	V_TMP_TIPO_DATA T_TIPO_DATA;
  
  
	SEL_ZON_ID VARCHAR2 (32000) := q'[SELECT ZON_ID FROM #ESQUEMA#.ZON_ZONIFICACION WHERE ZON_DESCRIPCION = 'REM']';
	
	SEL_PEF_ID VARCHAR2 (32000) := q'[SELECT PEF_ID FROM #ESQUEMA#.PEF_PERFILES WHERE PEF_CODIGO = :1]';
	
	SEL_USU_ID VARCHAR2 (32000) := q'[SELECT USU_ID FROM #ESQUEMA_MASTER#.USU_USUARIOS WHERE USU_USERNAME = :1]';
	
	SEL_USU_PASS VARCHAR2 (32000) := q'[SELECT USU_PASSWORD FROM #ESQUEMA_MASTER#.USU_USUARIOS WHERE USU_ID = :1]';
	
	SEL_USU_EMAIL VARCHAR2 (32000) := q'[SELECT USU_MAIL FROM #ESQUEMA_MASTER#.USU_USUARIOS WHERE USU_ID = :1]';
		
	SEL_DES_ID VARCHAR2(32000) := q'[SELECT DES_ID FROM #ESQUEMA#.DES_DESPACHO_EXTERNO WHERE DES_DESPACHO = :1]';
	
	SEL_GRU_ID VARCHAR2 (32000) := q'[SELECT USU_ID FROM #ESQUEMA_MASTER#.USU_USUARIOS WHERE USU_GRUPO = 1 AND USU_USERNAME = :1]';
	
	SEL_USU_GRUPO VARCHAR2(32000) := q'[SELECT USU_GRUPO FROM #ESQUEMA_MASTER#.USU_USUARIOS WHERE USU_ID = :1]';
	
	SEL_GRU_ID_CONT VARCHAR2 (32000) := q'[SELECT COUNT(1) FROM #ESQUEMA_MASTER#.USU_USUARIOS WHERE USU_USERNAME = :1 AND USU_GRUPO = 1]';
	
	SEL_PEF_USU_CONT VARCHAR2 (32000) := q'[SELECT COUNT(1) FROM #ESQUEMA#.ZON_PEF_USU WHERE PEF_ID = :1 AND USU_ID = :2]';
    
    SEL_PEF_CONT VARCHAR2 (32000) := q'[SELECT COUNT(1) FROM #ESQUEMA#.PEF_PERFILES WHERE PEF_CODIGO = :1]';
	
	SEL_DES_CONT VARCHAR (32000) := q'[SELECT COUNT(1) FROM #ESQUEMA#.DES_DESPACHO_EXTERNO WHERE DES_DESPACHO = :1]';
	
	SEL_USU_ID_CONT VARCHAR2 (32000) := q'[SELECT COUNT(1) FROM #ESQUEMA_MASTER#.USU_USUARIOS WHERE USU_USERNAME = :1]';
    
    SEL_DES_USU_CONT VARCHAR2 (32000) := q'[SELECT COUNT(1) FROM #ESQUEMA#.USD_USUARIOS_DESPACHOS WHERE USU_ID = :1 AND DES_ID = :2]';
	
	IN_USU_USUARIOS VARCHAR2 (32000):= q'[INSERT INTO #ESQUEMA_MASTER#.USU_USUARIOS (USU_ID, ENTIDAD_ID, USU_USERNAME,USU_NOMBRE,USU_PASSWORD,USU_APELLIDO1,USU_APELLIDO2,USU_MAIL,USU_GRUPO, USU_FECHA_VIGENCIA_PASS, USUARIOCREAR,FECHACREAR,BORRADO) VALUES
														(#ESQUEMA_MASTER#.S_USU_USUARIOS.nextval, 1, :1,:2,:3,:4,:5,:6,:7,SYSDATE+730,:8,SYSDATE,0)]';

	IN_ZON_PEF_USU VARCHAR2 (32000) := q'[INSERT INTO #ESQUEMA#.ZON_PEF_USU (ZPU_ID, ZON_ID, PEF_ID, USU_ID, USUARIOCREAR, FECHACREAR, BORRADO) VALUES
										(#ESQUEMA#.S_ZON_PEF_USU.NEXTVAL, :1,:2,:3,:4,SYSDATE,0)]';
																				
	IN_USU_DES VARCHAR2 (32000) := q'[INSERT INTO #ESQUEMA#.USD_USUARIOS_DESPACHOS (USD_ID, DES_ID, USU_ID, USD_GESTOR_DEFECTO, USD_SUPERVISOR, USUARIOCREAR, FECHACREAR, BORRADO) VALUES
										(#ESQUEMA#.S_USD_USUARIOS_DESPACHOS.NEXTVAL, :1, :2, :3, :4,:5, SYSDATE,0)]';
										
	IN_GRU_USU VARCHAR2 (32000) := q'[INSERT INTO #ESQUEMA_MASTER#.GRU_GRUPOS_USUARIOS (GRU_ID, USU_ID_GRUPO, USU_ID_USUARIO, USUARIOCREAR, FECHACREAR, BORRADO) VALUES
										(#ESQUEMA_MASTER#.S_GRU_GRUPOS_USUARIOS.NEXTVAL,:1,:2, :3,SYSDATE,0)]';
										
	UPD_GRU_USU VARCHAR2 (32000) := q'[UPDATE #ESQUEMA_MASTER#.USU_USUARIOS SET USU_GRUPO = 1 WHERE USU_ID = :1]';
	
	UPD_USU_PASS VARCHAR2 (32000) := q'[UPDATE #ESQUEMA_MASTER#.USU_USUARIOS SET USU_PASSWORD = :1 WHERE USU_ID = :2]';
	
	UPD_USU_EMAIL VARCHAR2 (32000) := q'[UPDATE #ESQUEMA_MASTER#.USU_USUARIOS SET USU_MAIL = :1 WHERE USU_ID = :2]';
	
	
BEGIN	
	DBMS_OUTPUT.PUT_LINE('[INICIO] ');
	FOR I IN V_TIPO_DATA.FIRST .. V_TIPO_DATA.LAST
    LOOP
        DBMS_OUTPUT.PUT_LINE('[USUARIO '||I||']');
		V_TMP_TIPO_DATA := V_TIPO_DATA(I);
		VV_USERNAME := V_TMP_TIPO_DATA(1);
		VV_NOMBRE := V_TMP_TIPO_DATA(2);
		VV_APELLIDO1 := V_TMP_TIPO_DATA(3);
		VV_APELLIDO2 := V_TMP_TIPO_DATA(4);
		VV_EMAIL := V_TMP_TIPO_DATA(5);
		VV_PASS := V_TMP_TIPO_DATA(6);
		VV_PEF := V_TMP_TIPO_DATA(7);
		VV_GRU := V_TMP_TIPO_DATA(8);
		VV_ES_GRU := V_TMP_TIPO_DATA(9);
		VV_DES := V_TMP_TIPO_DATA(10);
		VV_TICKET := V_TMP_TIPO_DATA(11);
		
		DBMS_OUTPUT.PUT_LINE('[INFO]: INSERCION EN USU_USUARIOS DE '||VV_USERNAME);
		EXECUTE IMMEDIATE SEL_USU_ID_CONT INTO V_CONT USING VV_USERNAME;
		IF V_CONT = 0 THEN
			EXECUTE IMMEDIATE IN_USU_USUARIOS USING VV_USERNAME, VV_NOMBRE, VV_PASS,  VV_APELLIDO1, VV_APELLIDO2, VV_EMAIL, VV_ES_GRU, VV_TICKET;
			DBMS_OUTPUT.PUT_LINE('[OK_USU_USUARIOS]:  USUARIO '||VV_USERNAME||'');
        ELSE 
            DBMS_OUTPUT.PUT_LINE('[NO_USU_USUARIOS]:  EL USUARIO '||VV_USERNAME||' YA EXISTE');
			EXECUTE IMMEDIATE SEL_USU_ID INTO V_USU_ID USING VV_USERNAME;
            DBMS_OUTPUT.PUT_LINE('[INFO]:  EL ID DEL USUARIO ES '||V_USU_ID||'');
			EXECUTE IMMEDIATE SEL_USU_PASS INTO V_AUX_VAR USING V_USU_ID;
            DBMS_OUTPUT.PUT_LINE('[INFO]:  LA PASS DEL USUARIO ES '||V_AUX_VAR||'');
			IF V_AUX_VAR IS NULL THEN 
				DBMS_OUTPUT.PUT_LINE('[INFO]: UPDATE PASSWORD DEL USUARIO '||VV_USERNAME);
				EXECUTE IMMEDIATE UPD_USU_PASS USING VV_PASS, V_USU_ID;
			END IF;
		END IF;
		
		--SELECCIONAMOS ID USUARIO
		EXECUTE IMMEDIATE SEL_USU_ID INTO V_USU_ID USING VV_USERNAME;
        
        --comprobamos si existe el perfil 
        EXECUTE IMMEDIATE SEL_PEF_CONT INTO V_CONT USING VV_PEF;
        IF V_CONT = 0 THEN 
            DBMS_OUTPUT.PUT_LINE('[KO]: EL PERFIL '||VV_PEF||' NO EXISTE');
        ELSE
            --Comprobamos si el usuario ya tiene ese perfil 
            DBMS_OUTPUT.PUT_LINE('[INFO]: INSERCION EN ZON_PEF_USU DE '||VV_USERNAME||' CON EL PERFIL '||VV_PEF);
            EXECUTE IMMEDIATE SEL_PEF_ID INTO V_PEF_ID USING VV_PEF;
            EXECUTE IMMEDIATE SEL_ZON_ID INTO V_ZON_ID;
            
            EXECUTE IMMEDIATE SEL_PEF_USU_CONT INTO V_CONT USING V_PEF_ID, V_USU_ID;
            IF V_CONT = 0 THEN
                EXECUTE IMMEDIATE IN_ZON_PEF_USU USING V_ZON_ID,V_PEF_ID,V_USU_ID,VV_TICKET;
                DBMS_OUTPUT.PUT_LINE('[OK_ZON_PEF_USU]: INSERCION EN ZON_PEF_USU DE '||VV_USERNAME||' CON EL PERFIL '||VV_PEF);
            ELSE DBMS_OUTPUT.PUT_LINE('[NO_PEF_PERFILES]:  EL USUARIO '||VV_USERNAME||' YA TIENE EL PERFIL '||VV_PEF);
            END IF;
		END IF;
        
        
		IF VV_DES IS NULL THEN 
			DBMS_OUTPUT.PUT_LINE('[NO_DES_DESPACHOS]: EL USUARIO '||VV_USERNAME||' NO INSERTA EN DESPACHOS');
		ELSE
			DBMS_OUTPUT.PUT_LINE('[INFO]: INSERCION EN USD_USUARIOS_DESPACHOS DE '||VV_USERNAME||' CON EL DESPACHO '||VV_DES);
			EXECUTE IMMEDIATE SEL_DES_CONT INTO V_CONT USING VV_DES;
			IF V_CONT = 1 THEN -- el despacho existe
				--comprobar que el usuario tiene despacho.
				EXECUTE IMMEDIATE SEL_DES_ID INTO V_DES_ID USING VV_DES;
				EXECUTE IMMEDIATE SEL_DES_USU_CONT INTO V_AUX USING V_USU_ID, V_DES_ID ;
				IF V_AUX = 0 THEN-- EL USUARIO NO ESTA EN EL DESPACHO
					EXECUTE IMMEDIATE IN_USU_DES USING V_DES_ID, V_USU_ID, USD_GESTOR_DEFECTO, USD_SUPERVISOR, VV_TICKET;
					DBMS_OUTPUT.PUT_LINE('[OK]: INSERCION EN USD_USUARIOS_DESPACHOS DE '||VV_USERNAME||' CON EL DESPACHO '||VV_DES);
				ELSE --EL USUARIO ESTA EN EL DESPACHO
					DBMS_OUTPUT.PUT_LINE('[NO_USD_USUARIOS_DESPACHOS]:  EL USUARIO '||VV_USERNAME||' YA TIENE EL DESPACHO '||VV_DES);
				END IF;

			ELSE DBMS_OUTPUT.PUT_LINE('[KO]: NO EXISTE EL DESPACHO '||VV_DES);
			END IF;
		END IF;
		
		--GRUPOS 
		
		IF VV_GRU IS NULL THEN --NO INSERTA EN GRUPO
			DBMS_OUTPUT.PUT_LINE('[NO_GRU_GRUPOS_USUARIOS]: EL USUARIO '||VV_USERNAME||' NO INSERTA EN GRUPOS ');
		ELSE --INSERTA EN GRUPO
			DBMS_OUTPUT.PUT_LINE('[INFO]: EL USUARIO '||VV_USERNAME||' INSERTA EN GRUPOS ');
			-- NUEVO USUARIO DE GRUPO
			IF VV_ES_GRU = 1 THEN
				DBMS_OUTPUT.PUT_LINE('[INFO]: EL USUARIO '||VV_USERNAME||' ES USUARIO DE GRUPO ');
					EXECUTE IMMEDIATE SEL_USU_GRUPO INTO V_AUX USING V_USU_ID;
					-- EXISTE COMO USUARIO DE GRUPO
					IF V_AUX = 1 THEN
						DBMS_OUTPUT.PUT_LINE('[INFO]: EL USUARIO '||VV_USERNAME||' YA EXISTIA Y ERA USUARIO DE GRUPO');
						EXECUTE IMMEDIATE IN_GRU_USU USING V_USU_ID, V_USU_ID, VV_TICKET;
						DBMS_OUTPUT.PUT_LINE('[OK]: EL USUARIO '||VV_USERNAME||' INSERTADO EN GRUPO '||VV_USERNAME);
					-- EXISTE PERO NO COMO USUARIO DE GRUPO 
					ELSE
						DBMS_OUTPUT.PUT_LINE('[INFO]: EL USUARIO '||VV_USERNAME||' YA EXISTIA Y NO ERA USUARIO DE GRUPO');
						EXECUTE IMMEDIATE UPD_GRU_USU USING V_USU_ID;
						DBMS_OUTPUT.PUT_LINE('[UPDATE]: USUARIO '||VV_USERNAME||' ACTUALIZADO A USUARIO DE GRUPO');
						EXECUTE IMMEDIATE IN_GRU_USU USING V_USU_ID, V_USU_ID, VV_TICKET;
						DBMS_OUTPUT.PUT_LINE('[OK]: EL USUARIO '||VV_USERNAME||' INSERTADO EN GRUPO '||VV_USERNAME);
					END IF;
			-- NO ES USUARIO DE GRUPO
			ELSE
				DBMS_OUTPUT.PUT_LINE('[INFO]: EL USUARIO '||VV_USERNAME||' NO ES USUARIO DE GRUPO ');
				--COMPROBAMOS QUE EL GRUPO EN EL QUE INSERTA ES GRUPO
				EXECUTE IMMEDIATE SEL_USU_ID INTO V_GRU_ID USING VV_GRU;
				EXECUTE IMMEDIATE SEL_USU_GRUPO INTO V_AUX USING V_GRU_ID;
				-- SI EL GRUPO EN EL QUE INSERTA ES GRUPO 
				IF V_AUX = 1 THEN
					DBMS_OUTPUT.PUT_LINE('[INFO]: EL USUARIO '||VV_USERNAME||' INSERTA EN '||VV_GRU||' QUE ES USUARIO DE GRUPO ');
					EXECUTE IMMEDIATE IN_GRU_USU USING V_GRU_ID, V_USU_ID, VV_TICKET;
					DBMS_OUTPUT.PUT_LINE('[OK]: EL USUARIO '||VV_USERNAME||' INSERTADO EN GRUPO '||VV_GRU);			
				--EL GRUPO EN EL QUE INSERTA NO ES GRUPO
				ELSE
					DBMS_OUTPUT.PUT_LINE('[INFO]: EL USUARIO '||VV_USERNAME||' INSERTA EN '||VV_GRU||' QUE NO ES USUARIO DE GRUPO ');
					EXECUTE IMMEDIATE UPD_GRU_USU USING V_GRU_ID;
					DBMS_OUTPUT.PUT_LINE('[UPDATE]: USUARIO '||VV_GRU||' ACTUALIZADO A USUARIO DE GRUPO');
					EXECUTE IMMEDIATE IN_GRU_USU USING V_GRU_ID, V_USU_ID, VV_TICKET;
					DBMS_OUTPUT.PUT_LINE('[OK]: EL USUARIO '||VV_USERNAME||' INSERTADO EN GRUPO '||VV_GRU);				
				END IF;
			END IF;
		END IF;
	END LOOP;
    
	COMMIT;
	DBMS_OUTPUT.PUT_LINE('[FIN]: PROCESO ACTUALIZADO CORRECTAMENTE ');
 

EXCEPTION

   WHEN OTHERS THEN
        err_num := SQLCODE;
        err_msg := SQLERRM;

        DBMS_OUTPUT.put_line('[ERROR] Se ha producido un error en la ejecución:'||TO_CHAR(err_num));
        DBMS_OUTPUT.put_line('-----------------------------------------------------------'); 
        DBMS_OUTPUT.put_line(err_msg);

        ROLLBACK;
        RAISE;          

END;

/

EXIT
