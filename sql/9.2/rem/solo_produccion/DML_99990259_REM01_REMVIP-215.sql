--/*
--#########################################
--## AUTOR=DAP
--## FECHA_CREACION=20180308
--## ARTEFACTO=batch
--## VERSION_ARTEFACTO=2.0.15
--## INCIDENCIA_LINK=REMVIP-215
--## PRODUCTO=NO
--## 
--## Finalidad: 
--##            
--## INSTRUCCIONES:  
--## VERSIONES:
--##        0.1 Versión inicial
--#########################################
--*/
--Para permitir la visualización de texto en un bloque PL/SQL utilizando DBMS_OUTPUT.PUT_LINE
WHENEVER SQLERROR EXIT SQL.SQLCODE;
SET SERVEROUTPUT ON;
SET DEFINE OFF;

DECLARE

	V_ESQUEMA VARCHAR2(25 CHAR):= '#ESQUEMA#';-- '#ESQUEMA#'; -- Configuracion Esquema
	V_ESQUEMA_M VARCHAR2(25 CHAR):= '#ESQUEMA_MASTER#';-- '#ESQUEMA_MASTER#'; -- Configuracion Esquema Master
	ERR_NUM NUMBER;-- Numero de errores
	ERR_MSG VARCHAR2(2048);-- Mensaje de error
	V_MSQL VARCHAR2(4000 CHAR);
	V_USUARIO VARCHAR2(50 CHAR) := 'REMVIP-215';
	TYPE T_TIPO_DATA IS TABLE OF VARCHAR2(150);
	TYPE T_ARRAY_DATA IS TABLE OF T_TIPO_DATA;
	V_TIPO_DATA T_ARRAY_DATA := T_ARRAY_DATA(
		T_TIPO_DATA('zmartin','acampos','GFORM'),
		T_TIPO_DATA('cmartinez','nbertran','GFORM'));
	V_TMP_TIPO_DATA T_TIPO_DATA;

BEGIN

    DBMS_OUTPUT.PUT_LINE('[INICIO] Inicio cambio gestores');
    
    FOR I IN V_TIPO_DATA.FIRST .. V_TIPO_DATA.LAST
    	LOOP
	    	V_TMP_TIPO_DATA := V_TIPO_DATA(I);
	    	DBMS_OUTPUT.PUT_LINE('	[INFO] Cambiamos gestores de ofertas y tareas '||V_TMP_TIPO_DATA(1)||' por '||V_TMP_TIPO_DATA(2));
	    	V_USUARIO := V_USUARIO||'_'||I;
			V_MSQL := 'DELETE FROM '||V_ESQUEMA||'.AUX_GCOM';
			EXECUTE IMMEDIATE V_MSQL;
			DBMS_OUTPUT.PUT_LINE('	[INFO] Limpiamos tabla AUX_GCOM');

			V_MSQL := 'MERGE INTO '||V_ESQUEMA||'.TAC_TAREAS_ACTIVOS T1
				USING (SELECT TAC.TAR_ID, TAC.TRA_ID, TAC.ACT_ID, '''||V_TMP_TIPO_DATA(2)||''' USERNAME
				    FROM '||V_ESQUEMA||'.TAC_TAREAS_ACTIVOS TAC
				    JOIN '||V_ESQUEMA||'.TAR_TAREAS_NOTIFICACIONES TAR ON TAR.TAR_ID = TAC.TAR_ID
				    JOIN '||V_ESQUEMA||'.ACT_TRA_TRAMITE TRA ON TRA.TRA_ID = TAC.TRA_ID
				    JOIN '||V_ESQUEMA||'.ECO_EXPEDIENTE_COMERCIAL ECO ON ECO.TBJ_ID = TRA.TBJ_ID
				    JOIN '||V_ESQUEMA_M||'.USU_USUARIOS USU ON USU.USU_ID = TAC.USU_ID
				        AND USU.USU_USERNAME = '''||V_TMP_TIPO_DATA(1)||'''
				    WHERE TAR.BORRADO = 0 OR TAR.TAR_FECHA_FIN IS NULL OR NVL(TAR.TAR_TAREA_FINALIZADA,0) = 0) T2
				ON (T1.TAR_ID = T2.TAR_ID AND T1.TRA_ID = T2.TRA_ID AND T1.ACT_ID = T2.ACT_ID)
				WHEN MATCHED THEN UPDATE SET
				    T1.USU_ID = (SELECT USU_ID FROM '||V_ESQUEMA_M||'.USU_USUARIOS WHERE USU_USERNAME = T2.USERNAME)';
			EXECUTE IMMEDIATE V_MSQL;
			DBMS_OUTPUT.PUT_LINE('	[INFO] Cambiamos '||SQL%ROWCOUNT||' gestores de las tareas desde '||V_TMP_TIPO_DATA(1)||' a '||V_TMP_TIPO_DATA(2));

			V_MSQL := 'INSERT INTO '||V_ESQUEMA||'.AUX_GCOM (ACT, USU, TGE)
				SELECT DISTINCT ECO.ECO_ID, USU.USU_ID, TGE.DD_TGE_ID
				FROM '||V_ESQUEMA||'.ECO_EXPEDIENTE_COMERCIAL ECO
				JOIN '||V_ESQUEMA||'.GCH_GESTOR_ECO_HISTORICO GCH ON GCH.ECO_ID = ECO.ECO_ID
				JOIN '||V_ESQUEMA||'.GEH_GESTOR_ENTIDAD_HIST GEH ON GEH.GEH_ID = GCH.GEH_ID AND GEH.GEH_FECHA_HASTA IS NULL
				    AND GEH.BORRADO = 0 
				JOIN '||V_ESQUEMA_M||'.DD_TGE_TIPO_GESTOR TGE ON TGE.DD_TGE_ID = GEH.DD_TGE_ID AND TGE.DD_TGE_CODIGO = '''||V_TMP_TIPO_DATA(3)||'''
				JOIN '||V_ESQUEMA_M||'.USU_USUARIOS USU ON USU.USU_ID = GEH.USU_ID AND USU.USU_USERNAME = '''||V_TMP_TIPO_DATA(1)||'''';
			EXECUTE IMMEDIATE V_MSQL;
			DBMS_OUTPUT.PUT_LINE('	[INFO] Insertamos '||SQL%ROWCOUNT||' filas en tabla AUX_GCOM');

			V_MSQL := 'MERGE INTO '||V_ESQUEMA||'.GEE_GESTOR_ENTIDAD T1
				USING (
				    SELECT ECO_ID, GEE_ID, DD_TGE_ID
				    FROM (SELECT AUX.TGE DD_TGE_ID, AUX.ACT ECO_ID, GEE.GEE_ID, ROW_NUMBER() OVER(PARTITION BY AUX.ACT ORDER BY GEE.DD_TGE_ID NULLS LAST) RN
				        FROM '||V_ESQUEMA||'.AUX_GCOM AUX
				        LEFT JOIN '||V_ESQUEMA||'.GCO_GESTOR_ADD_ECO GCO ON GCO.ECO_ID = AUX.ACT
				        LEFT JOIN '||V_ESQUEMA||'.GEE_GESTOR_ENTIDAD GEE ON GEE.GEE_ID = GCO.GEE_ID AND GEE.DD_TGE_ID = AUX.TGE
				            AND GEE.USU_ID = AUX.USU)
				    WHERE RN = 1) T2
				ON (T1.GEE_ID = T2.GEE_ID)
				WHEN MATCHED THEN UPDATE SET
				    T1.USU_ID = (SELECT USU_ID FROM '||V_ESQUEMA_M||'.USU_USUARIOS WHERE USU_USERNAME = '''||V_TMP_TIPO_DATA(2)||''')
				    , T1.USUARIOMODIFICAR = '''||V_USUARIO||''', T1.FECHAMODIFICAR = SYSDATE
				WHERE T1.USU_ID <> (SELECT USU_ID FROM '||V_ESQUEMA_M||'.USU_USUARIOS WHERE USU_USERNAME = '''||V_TMP_TIPO_DATA(2)||''')
				WHEN NOT MATCHED THEN INSERT (GEE_ID, USU_ID, DD_TGE_ID, USUARIOCREAR, FECHACREAR, USUARIOMODIFICAR)
				    VALUES ('||V_ESQUEMA||'.S_GEE_GESTOR_ENTIDAD.NEXTVAL, (SELECT USU_ID FROM '||V_ESQUEMA_M||'.USU_USUARIOS WHERE USU_USERNAME = '''||V_TMP_TIPO_DATA(2)||''')
				        , T2.DD_TGE_ID, '''||V_USUARIO||''', SYSDATE, T2.ECO_ID)';
			EXECUTE IMMEDIATE V_MSQL;
			DBMS_OUTPUT.PUT_LINE('	[INFO] Fusionamos '||SQL%ROWCOUNT||' gestores en tabla GEE');
			            
			V_MSQL := 'INSERT INTO '||V_ESQUEMA||'.GCO_GESTOR_ADD_ECO (GEE_ID, ECO_ID)
				SELECT GEE_ID, TO_NUMBER(USUARIOMODIFICAR)
				FROM '||V_ESQUEMA||'.GEE_GESTOR_ENTIDAD
				WHERE USUARIOCREAR = '''||V_USUARIO||''' AND TRUNC(FECHACREAR) = TRUNC(SYSDATE)';
			EXECUTE IMMEDIATE V_MSQL;
			DBMS_OUTPUT.PUT_LINE('	[INFO] Insertamos '||SQL%ROWCOUNT||' gestores en GCO');

			V_MSQL := 'MERGE INTO '||V_ESQUEMA||'.GEH_GESTOR_ENTIDAD_HIST T1
				USING (
				    SELECT GEH.GEH_ID
				    FROM '||V_ESQUEMA||'.AUX_GCOM AUX
				    JOIN '||V_ESQUEMA||'.GCH_GESTOR_ECO_HISTORICO GCH ON GCH.ECO_ID = AUX.ACT
				    JOIN '||V_ESQUEMA||'.GEH_GESTOR_ENTIDAD_HIST GEH ON GEH.GEH_ID = GCH.GEH_ID AND GEH.DD_TGE_ID = AUX.TGE
				        AND GEH.GEH_FECHA_HASTA IS NULL AND AUX.USU = GEH.USU_ID) T2
				ON (T1.GEH_ID = T2.GEH_ID)
				WHEN MATCHED THEN UPDATE SET
				    T1.GEH_FECHA_HASTA = SYSDATE, T1.USUARIOMODIFICAR = '''||V_USUARIO||''', T1.FECHAMODIFICAR = SYSDATE';
			EXECUTE IMMEDIATE V_MSQL;
			DBMS_OUTPUT.PUT_LINE('	[INFO] Actualizamos '||SQL%ROWCOUNT||' gestores históricos en tabla GEH');
			    
			V_MSQL := 'INSERT INTO '||V_ESQUEMA||'.GEH_GESTOR_ENTIDAD_HIST (GEH_ID, USU_ID, DD_TGE_ID
				    , GEH_FECHA_DESDE, USUARIOCREAR, FECHACREAR, USUARIOMODIFICAR)
				SELECT '||V_ESQUEMA||'.S_GEH_GESTOR_ENTIDAD_HIST.NEXTVAL, USU.USU_ID, AUX.TGE
				    , SYSDATE, '''||V_USUARIO||''', SYSDATE, AUX.ACT
				FROM '||V_ESQUEMA||'.AUX_GCOM AUX
				JOIN '||V_ESQUEMA_M||'.USU_USUARIOS USU ON USU.USU_USERNAME = '''||V_TMP_TIPO_DATA(2)||'''
				WHERE NOT EXISTS (SELECT 1
				    FROM '||V_ESQUEMA||'.GEH_GESTOR_ENTIDAD_HIST GEH
				    JOIN '||V_ESQUEMA||'.GCH_GESTOR_ECO_HISTORICO GCH ON GCH.GEH_ID = GEH.GEH_ID
				    WHERE GEH.GEH_FECHA_HASTA IS NULL AND AUX.TGE = GEH.DD_TGE_ID
				        AND AUX.ACT = GCH.ECO_ID AND USU.USU_ID = GEH.USU_ID)';
			EXECUTE IMMEDIATE V_MSQL;
			DBMS_OUTPUT.PUT_LINE('	[INFO] Insertamos '||SQL%ROWCOUNT||' gestores históricos en tabla GEH');
			        
			V_MSQL := 'INSERT INTO '||V_ESQUEMA||'.GCH_GESTOR_ECO_HISTORICO (GEH_ID, ECO_ID)
				SELECT GEH_ID, TO_NUMBER(USUARIOMODIFICAR)
				FROM '||V_ESQUEMA||'.GEH_GESTOR_ENTIDAD_HIST
				WHERE USUARIOCREAR = '''||V_USUARIO||''' AND TRUNC(FECHACREAR) = TRUNC(SYSDATE)';
			EXECUTE IMMEDIATE V_MSQL;
			DBMS_OUTPUT.PUT_LINE('	[INFO] Insertamos '||SQL%ROWCOUNT||' gestores históricos en la tabla ECO');
    
        END LOOP;
        
	COMMIT;
    
    DBMS_OUTPUT.PUT_LINE('[FIN]');

EXCEPTION
    WHEN OTHERS THEN
      DBMS_OUTPUT.PUT_LINE('[ERROR] Se ha producido un error en la ejecución:'||TO_CHAR(SQLCODE));
      DBMS_OUTPUT.PUT_LINE('-----------------------------------------------------------');
      DBMS_OUTPUT.PUT_LINE(SQLERRM);
      DBMS_OUTPUT.PUT_LINE(V_MSQL);
      ROLLBACK;
      RAISE;
END;
/
EXIT;
