--/*
--#########################################
--## AUTOR=PIER GOTTA
--## FECHA_CREACION=20210727
--## ARTEFACTO=batch
--## VERSION_ARTEFACTO=0.11
--## INCIDENCIA_LINK=HREOS-14680
--## PRODUCTO=NO
--## 
--## Finalidad: 
--## 
--## INSTRUCCIONES:  
--## VERSIONES:
--## 	0.1 Versión inicial
--#########################################
--*/

--Para permitir la visualización de texto en un bloque PL/SQL utilizando DBMS_OUTPUT.PUT_LINE

WHENEVER SQLERROR EXIT SQL.SQLCODE;
SET SERVEROUTPUT ON;
SET DEFINE OFF;

DECLARE

	V_ESQUEMA VARCHAR2(10 CHAR) := 'REM01';
	V_ESQUEMA_MASTER VARCHAR2(15 CHAR) := 'REMMASTER';
	V_USUARIO VARCHAR2(50 CHAR) := 'MIG_CAIXA';
	V_TABLA VARCHAR2(40 CHAR) := 'OFR_TIA_TITULARES_ADICIONALES';
	V_TABLA_MIG VARCHAR2(40 CHAR) := 'MIG2_OFR_TIA_TITULARES_ADI_CAIXA';
	V_SENTENCIA VARCHAR2(2000 CHAR);
	V_NUM_TABLAS NUMBER(16);
	V_MSQL VARCHAR2(32000 CHAR); -- Vble. para consulta que valida la existencia de una tabla.

BEGIN

	--Inicio del proceso de volcado sobre OFR_TIA_TITULARES_ADICIONALES
V_MSQL := 'SELECT COUNT(1) FROM '||V_ESQUEMA||'.'||V_TABLA_MIG||'';

EXECUTE IMMEDIATE V_MSQL INTO V_NUM_TABLAS;

IF V_NUM_TABLAS = 0 THEN
DBMS_OUTPUT.PUT_LINE('La tabla/s de migración implicada está vacía. No se realiza ninguna acción');

ELSE

	DBMS_OUTPUT.PUT_LINE('[INFO] COMIENZA EL PROCESO DE MIGRACION SOBRE LA TABLA '||V_ESQUEMA||'.'||V_TABLA||'.');

	EXECUTE IMMEDIATE '
	INSERT INTO '||V_ESQUEMA||'.'||V_TABLA||' (
		TIA_ID,
		OFR_ID,
		TIA_NOMBRE,
		DD_TDI_ID,
		TIA_DOCUMENTO,
		TIA_APELLIDOS, 				/* Añadidos para Divarian */
		TIA_DIRECCION,
		DD_LOC_ID,
		DD_PRV_ID,
		TIA_CODPOSTAL,
		DD_ECV_ID,
		DD_REM_ID,
		TIA_RECHAZAR_PUBLI,
		TIA_RECHAZAR_PROPI,
		TIA_RECHAZAR_PROVE,
		TIA_RAZON_SOCIAL,
		TIA_TELEFONO1,
		TIA_TELEFONO2,
		TIA_EMAIL,
		DD_TVI_ID,
		TIA_OBSERVACIONES,
		DD_TDI_ID_CONYUGE,
		TIA_DOCUMENTO_CONYUGE,
		DD_TPE_ID,
		DD_PAI_ID,
		DD_TDI_ID_RTE,
		TIA_DOCUMENTO_RTE,
		TIA_DIRECCION_RTE,
		DD_PRV_ID_RTE,
		DD_LOC_ID_RTE,
		DD_PAI_ID_RTE,
		TIA_CODPOSTAL_RTE,
		VERSION,
		USUARIOCREAR,
		FECHACREAR,
		BORRADO
	)
	WITH INSERTAR AS (
		SELECT DISTINCT 
			OFR.OFR_ID,
			MIG.OFR_TIA_NOMBRE,
			MIG.OFR_TIA_COD_TIPO_DOC_TITUL_ADI,
			MIG.OFR_TIA_DOCUMENTO,
			MIG.TIA_APELLIDOS, 			/* Añadidos para Divarian */
			MIG.TIA_DIRECCION,
			MIG.DD_LOC_COD,
			MIG.DD_PRV_COD,
			MIG.TIA_CODPOSTAL,
			MIG.DD_ECV_COD,
			MIG.DD_REM_COD,
			MIG.TIA_RECHAZAR_PUBLI,
			MIG.TIA_RECHAZAR_PROPI,
			MIG.TIA_RECHAZAR_PROVE,
			MIG.TIA_RAZON_SOCIAL,
			MIG.TIA_TELEFONO1,
			MIG.TIA_TELEFONO2,
			MIG.TIA_EMAIL,
			MIG.DD_TVI_COD,
			MIG.TIA_OBSERVACIONES,
			MIG.DD_TDI_COD_CONYUGE,
			MIG.TIA_DOCUMENTO_CONYUGE,
			MIG.DD_TPE_COD,
			MIG.DD_PAI_COD,
			MIG.DD_TDI_COD_RTE,
			MIG.TIA_DOCUMENTO_RTE,
			MIG.TIA_DIRECCION_RTE,
			MIG.DD_PRV_COD_RTE,
			MIG.DD_LOC_COD_RTE,
			MIG.DD_PAI_COD_RTE,
			MIG.TIA_CODPOSTAL_RTE 
		FROM '||V_ESQUEMA||'.'||V_TABLA_MIG||' MIG
		INNER JOIN '||V_ESQUEMA||'.OFR_OFERTAS OFR ON OFR.OFR_NUM_OFERTA = MIG.OFR_TIA_COD_OFERTA
		WHERE MIG.VALIDACION = 0
	)
	SELECT
		'||V_ESQUEMA||'.S_OFR_TIA_TIT_ADICIONALES.NEXTVAL	TIA_ID,
		TIA.OFR_ID											OFR_ID,
		TIA.OFR_TIA_NOMBRE									TIA_NOMBRE,
		TDI.DD_TDI_ID 										DD_TDI_ID,
		TIA.OFR_TIA_DOCUMENTO								TIA_DOCUMENTO,
		TIA.TIA_APELLIDOS 									TIA_APELLIDOS, 				/* Añadido para Divarian */
		TIA.TIA_DIRECCION 									TIA_DIRECCION,
		LOC.DD_LOC_ID 										DD_LOC_ID,
		PRV.DD_PRV_ID 										DD_PRV_ID,
		TIA.TIA_CODPOSTAL 									TIA_CODPOSTAL,
		ECV.DD_ECV_ID 										DD_ECV_ID,
		REM.DD_REM_ID 										DD_REM_ID,
		TIA.TIA_RECHAZAR_PUBLI 								TIA_RECHAZAR_PUBLI,
		TIA.TIA_RECHAZAR_PROPI 								TIA_RECHAZAR_PROPI,
		TIA.TIA_RECHAZAR_PROVE 								TIA_RECHAZAR_PROVE,
		TIA.TIA_RAZON_SOCIAL 								TIA_RAZON_SOCIAL,
		TIA.TIA_TELEFONO1 									TIA_TELEFONO1,
		TIA.TIA_TELEFONO2 									TIA_TELEFONO2,
		TIA.TIA_EMAIL 										TIA_EMAIL,
		TVI.DD_TVI_ID 										DD_TVI_ID,
		TIA.TIA_OBSERVACIONES 								TIA_OBSERVACIONES,
		TDI_2.DD_TDI_ID 									DD_TDI_ID_CONYUGE,
		TIA.TIA_DOCUMENTO_CONYUGE 							TIA_DOCUMENTO_CONYUGE,
		TPE.DD_TPE_ID 										DD_TPE_ID,
		PAI.DD_PAI_ID 										DD_PAI_ID,
		TDI_3.DD_TDI_ID 									DD_TDI_ID_RTE,
		TIA.TIA_DOCUMENTO_RTE 								TIA_DOCUMENTO_RTE,
		TIA.TIA_DIRECCION_RTE 								TIA_DIRECCION_RTE,
		PRV_2.DD_PRV_ID 									DD_PRV_ID_RTE,
		LOC_2.DD_LOC_ID 									DD_LOC_ID_RTE,
		PAI_2.DD_PAI_ID 									DD_PAI_ID_RTE,
		TIA.TIA_CODPOSTAL_RTE 								TIA_CODPOSTAL_RTE,
		''0'' 												VERSION,
		'''||V_USUARIO||''' 								USUARIOCREAR,
		SYSDATE												FECHACREAR,
		0													BORRADO
	FROM INSERTAR TIA
	LEFT JOIN '||V_ESQUEMA||'.DD_TDI_TIPO_DOCUMENTO_ID 			TDI 	ON TDI.DD_TDI_CODIGO = TIA.OFR_TIA_COD_TIPO_DOC_TITUL_ADI
	LEFT JOIN '||V_ESQUEMA_MASTER||'.DD_LOC_LOCALIDAD 			LOC 	ON LOC.DD_LOC_CODIGO = TIA.DD_LOC_COD
	LEFT JOIN '||V_ESQUEMA_MASTER||'.DD_PRV_PROVINCIA 			PRV 	ON PRV.DD_PRV_CODIGO = TIA.DD_PRV_COD
	LEFT JOIN '||V_ESQUEMA||'.DD_ECV_ESTADOS_CIVILES 			ECV 	ON ECV.DD_ECV_CODIGO = TIA.DD_ECV_COD
	LEFT JOIN '||V_ESQUEMA||'.DD_REM_REGIMENES_MATRIMONIALES 	REM 	ON REM.DD_REM_CODIGO = TIA.DD_REM_COD
	LEFT JOIN '||V_ESQUEMA_MASTER||'.DD_TVI_TIPO_VIA 			TVI 	ON TVI.DD_TVI_CODIGO = TIA.DD_TVI_COD
	LEFT JOIN '||V_ESQUEMA||'.DD_TDI_TIPO_DOCUMENTO_ID 			TDI_2 	ON TDI_2.DD_TDI_CODIGO = TIA.DD_TDI_COD_CONYUGE
	LEFT JOIN '||V_ESQUEMA||'.DD_TPE_TIPO_PERSONA 				TPE 	ON TPE.DD_TPE_CODIGO = TIA.DD_TPE_COD
	LEFT JOIN '||V_ESQUEMA||'.DD_PAI_PAISES 					PAI 	ON PAI.DD_PAI_CODIGO = TIA.DD_PAI_COD
	LEFT JOIN '||V_ESQUEMA||'.DD_TDI_TIPO_DOCUMENTO_ID 			TDI_3 	ON TDI_3.DD_TDI_CODIGO = TIA.DD_TDI_COD_RTE
	LEFT JOIN '||V_ESQUEMA_MASTER||'.DD_PRV_PROVINCIA 			PRV_2 	ON PRV_2.DD_PRV_CODIGO = TIA.DD_PRV_COD_RTE
	LEFT JOIN '||V_ESQUEMA_MASTER||'.DD_LOC_LOCALIDAD 			LOC_2 	ON LOC_2.DD_LOC_CODIGO = TIA.DD_LOC_COD_RTE
	LEFT JOIN '||V_ESQUEMA||'.DD_PAI_PAISES 					PAI_2 	ON PAI_2.DD_PAI_CODIGO = TIA.DD_PAI_COD_RTE
	';
	
	DBMS_OUTPUT.PUT_LINE('[INFO] - '||to_char(sysdate,'HH24:MI:SS')||' '||V_ESQUEMA||'.'||V_TABLA||' cargada. '||SQL%ROWCOUNT||' Filas.');
	
	COMMIT;
	
	V_SENTENCIA := 'BEGIN '||V_ESQUEMA||'.OPERACION_DDL.DDL_TABLE(''ANALYZE'','''||V_TABLA||''',''10''); END;';
	EXECUTE IMMEDIATE V_SENTENCIA;
	DBMS_OUTPUT.PUT_LINE('[INFO] '||V_ESQUEMA||'.'||V_TABLA||' ANALIZADA.');
END IF;
EXCEPTION
	WHEN OTHERS THEN
		DBMS_OUTPUT.put_line('[ERROR] Se ha producido un error en la ejecucion:'||TO_CHAR(SQLCODE));
		DBMS_OUTPUT.put_line('-----------------------------------------------------------');
		DBMS_OUTPUT.put_line(SQLERRM);
		ROLLBACK;
		RAISE;
END;
/
EXIT;
