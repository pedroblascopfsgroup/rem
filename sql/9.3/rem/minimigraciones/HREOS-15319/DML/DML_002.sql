--/*
--#########################################
--## AUTOR=Daniel Algaba
--## FECHA_CREACION=20210721
--## ARTEFACTO=online
--## VERSION_ARTEFACTO=9.3
--## INCIDENCIA_LINK=HREOS-15319
--## PRODUCTO=NO
--## 
--## Finalidad: Actualización
--## INSTRUCCIONES:  
--## VERSIONES:
--##        0.1 Versión inicial
--#########################################
--*/

WHENEVER SQLERROR EXIT SQL.SQLCODE;
SET SERVEROUTPUT ON;
SET DEFINE OFF;

DECLARE
	
	V_TABLA_TMP VARCHAR2(30 CHAR) := 'APR_AUX_HREOS_15319'; -- Variable para tabla de salida para el borrado	
	V_TABLA VARCHAR2(30 CHAR) := 'ACT_PVE_PROVEEDOR'; -- Variable para tabla de salida para el borrado	
	V_ESQUEMA VARCHAR2(25 CHAR):= 'REM01';-- '#ESQUEMA#'; -- Configuracion Esquema
	V_ESQUEMA_M VARCHAR2(25 CHAR):= 'REMMASTER';-- '#ESQUEMA_MASTER#'; -- Configuracion Esquema Master
	ERR_NUM NUMBER;-- Numero de errores
	ERR_MSG VARCHAR2(2048);-- Mensaje de error	
	V_SQL VARCHAR2(4000 CHAR);--Sentencia a ejecutar	
	PL_OUTPUT VARCHAR2(32000 CHAR);
	V_USUARIO VARCHAR2(50 CHAR) := 'HREOS-15319';
	
BEGIN

	DBMS_OUTPUT.PUT_LINE('[INICIO]'||CHR(10));
	DBMS_OUTPUT.PUT_LINE('[INFO] FUSIONANDO LOS DATOS DE LA TABLA '||V_ESQUEMA||'.'||V_TABLA_TMP||' EN LA TABLA '||V_TABLA||'');

	V_SQL := 'MERGE INTO '||V_ESQUEMA||'.'||V_TABLA||' PVE
				USING(
					SELECT
					PVE.PVE_ID
					, AUX.COD_CAIXABANK PVE_COD_API_PROVEEDOR
					, AUX.NOMBRE_CAIXABANK PVE_NOMBRE
					FROM '||V_ESQUEMA||'.'||V_TABLA_TMP||' AUX
					LEFT JOIN (SELECT PVE.PVE_ID, PVE.PVE_COD_API_PROVEEDOR 
					FROM '||V_ESQUEMA||'.ACT_PVE_PROVEEDOR PVE 
					JOIN '||V_ESQUEMA||'.DD_TPR_TIPO_PROVEEDOR TPR ON TPR.DD_TPR_ID = PVE.DD_TPR_ID AND TPR.BORRADO = 0 AND TPR.DD_TPR_CODIGO = ''28''
                    WHERE PVE.BORRADO = 0) PVE ON AUX.COD_BANKIA = PVE.PVE_COD_API_PROVEEDOR 
				) AUX
				ON (PVE.PVE_ID = AUX.PVE_ID)
				WHEN MATCHED THEN
				UPDATE SET
					PVE.PVE_COD_API_PROVEEDOR = LPAD(AUX.PVE_COD_API_PROVEEDOR, 4 ,''0'')
					, PVE.PVE_NOMBRE = AUX.PVE_NOMBRE 
					, PVE.PVE_NOMBRE_COMERCIAL = AUX.PVE_NOMBRE 
					, PVE.DD_TDI_ID = (SELECT DD_TDI_ID FROM DD_TDI_TIPO_DOCUMENTO_ID WHERE DD_TDI_CODIGO = ''15'')
					, PVE.PVE_DOCIDENTIF = ''A08663619''
					, PVE.PVE_EMAIL = ''oficina.'' || LPAD(AUX.PVE_COD_API_PROVEEDOR, 4 ,''0'') || ''@caixabank.es''
					, PVE.PVE_FECHA_BAJA = NULL
					, PVE.DD_EPR_ID = (SELECT DD_EPR_ID FROM '||V_ESQUEMA||'.DD_EPR_ESTADO_PROVEEDOR WHERE DD_EPR_CODIGO = ''04'')
					, PVE.DD_TPE_ID = (SELECT DD_TPE_ID FROM '||V_ESQUEMA_M||'.DD_TPE_TIPO_PERSONA WHERE DD_TPE_CODIGO = ''2'')
					, PVE.USUARIOMODIFICAR = '''||V_USUARIO||'''
					, PVE.FECHAMODIFICAR = SYSDATE
				WHEN NOT MATCHED THEN
					INSERT (PVE_ID
					, PVE_COD_API_PROVEEDOR
					, PVE_NOMBRE
					, PVE_NOMBRE_COMERCIAL
					, DD_TDI_ID
					, PVE_DOCIDENTIF
					, PVE_EMAIL
					, DD_EPR_ID
					, DD_TPE_ID
					, DD_TPR_ID
					, PVE_COD_REM
					, USUARIOCREAR
					, FECHACREAR)
					VALUES (
					'||V_ESQUEMA||'.S_'||V_TABLA||'.NEXTVAL
					, LPAD(AUX.PVE_COD_API_PROVEEDOR, 4 ,''0'')
					, AUX.PVE_NOMBRE
					, AUX.PVE_NOMBRE
					, (SELECT DD_TDI_ID FROM DD_TDI_TIPO_DOCUMENTO_ID WHERE DD_TDI_CODIGO = ''15'')
					, ''A08663619''
					, ''oficina.'' || LPAD(AUX.PVE_COD_API_PROVEEDOR, 4 ,''0'') || ''@caixabank.es''
					, (SELECT DD_EPR_ID FROM '||V_ESQUEMA||'.DD_EPR_ESTADO_PROVEEDOR WHERE DD_EPR_CODIGO = ''04'')
					, (SELECT DD_TPE_ID FROM '||V_ESQUEMA_M||'.DD_TPE_TIPO_PERSONA WHERE DD_TPE_CODIGO = ''2'')
					, (SELECT DD_TPR_ID FROM '||V_ESQUEMA||'.DD_TPR_TIPO_PROVEEDOR WHERE DD_TPR_CODIGO = ''28'')
					, '||V_ESQUEMA||'.S_PVE_COD_REM.NEXTVAL
					, '''||V_USUARIO||'''
					, SYSDATE)';
	DBMS_OUTPUT.PUT_LINE(V_SQL);
	EXECUTE IMMEDIATE V_SQL;
	DBMS_OUTPUT.PUT_LINE('[INFO] Se han fusionado un total de '||SQL%ROWCOUNT||' filas de la tabla '||V_TABLA);
	
	V_SQL := 'MERGE INTO '||V_ESQUEMA||'.'||V_TABLA||' PVE
				USING(
					SELECT
					PVE.PVE_ID
					FROM '||V_ESQUEMA||'.'||V_TABLA||' PVE  
					JOIN '||V_ESQUEMA||'.DD_TPR_TIPO_PROVEEDOR TPR ON TPR.DD_TPR_ID = PVE.DD_TPR_ID AND TPR.BORRADO = 0 AND TPR.DD_TPR_CODIGO = ''28''
					WHERE PVE.BORRADO = 0 
					AND (PVE.PVE_DOCIDENTIF <> ''A08663619'' OR PVE.PVE_DOCIDENTIF IS NULL)
				) AUX
				ON (PVE.PVE_ID = AUX.PVE_ID)
				WHEN MATCHED THEN
				UPDATE SET
					PVE.DD_EPR_ID = (SELECT DD_EPR_ID FROM '||V_ESQUEMA||'.DD_EPR_ESTADO_PROVEEDOR WHERE DD_EPR_CODIGO = ''07'')
					, PVE.PVE_FECHA_BAJA = SYSDATE
					, PVE.USUARIOMODIFICAR = '''||V_USUARIO||'''
					, PVE.FECHAMODIFICAR = SYSDATE';
	DBMS_OUTPUT.PUT_LINE(V_SQL);
	EXECUTE IMMEDIATE V_SQL;
	DBMS_OUTPUT.PUT_LINE('[INFO] Se han dado de baja un total de '||SQL%ROWCOUNT||' filas de la tabla '||V_TABLA);

	V_SQL := 'MERGE INTO '||V_ESQUEMA||'.ACT_ETP_ENTIDAD_PROVEEDOR ETP
				USING(
					SELECT
					PVE.PVE_ID
					FROM '||V_ESQUEMA||'.'||V_TABLA||' PVE
					JOIN '||V_ESQUEMA||'.DD_TPR_TIPO_PROVEEDOR TPR ON TPR.DD_TPR_ID = PVE.DD_TPR_ID AND TPR.BORRADO = 0 AND TPR.DD_TPR_CODIGO = ''28''
					WHERE PVE.BORRADO = 0
					AND PVE.PVE_FECHA_BAJA IS NULL 
					AND PVE.DD_EPR_ID = (SELECT DD_EPR_ID FROM '||V_ESQUEMA||'.DD_EPR_ESTADO_PROVEEDOR WHERE DD_EPR_CODIGO = ''04'')
				) AUX
				ON (ETP.PVE_ID = AUX.PVE_ID)
				WHEN MATCHED THEN
				UPDATE SET
					ETP.DD_CRA_ID = (SELECT DD_CRA_ID FROM DD_CRA_CARTERA WHERE DD_CRA_CODIGO = ''03'')
					, ETP.USUARIOMODIFICAR = '''||V_USUARIO||'''
					, ETP.FECHAMODIFICAR = SYSDATE
					WHERE ETP.DD_CRA_ID <> (SELECT DD_CRA_ID FROM DD_CRA_CARTERA WHERE DD_CRA_CODIGO = ''03'')
				WHEN NOT MATCHED THEN
					INSERT (ETP_ID
					, DD_CRA_ID
					, PVE_ID
					, USUARIOCREAR
					, FECHACREAR)
					VALUES ('||V_ESQUEMA||'.S_ACT_ETP_ENTIDAD_PROVEEDOR.NEXTVAL
					, (SELECT DD_CRA_ID FROM DD_CRA_CARTERA WHERE DD_CRA_CODIGO = ''03'')
					, AUX.PVE_ID
					, '''||V_USUARIO||'''
					, SYSDATE)';
	DBMS_OUTPUT.PUT_LINE(V_SQL);
	EXECUTE IMMEDIATE V_SQL;
	DBMS_OUTPUT.PUT_LINE('[INFO] Se han fusionado un total de '||SQL%ROWCOUNT||' filas de la tabla ACT_ETP_ENTIDAD_PROVEEDOR');

	COMMIT;
	
	PL_OUTPUT := PL_OUTPUT || '[FIN]'||CHR(10);
	DBMS_OUTPUT.PUT_LINE(PL_OUTPUT);

EXCEPTION
    WHEN OTHERS THEN
      PL_OUTPUT := PL_OUTPUT ||'[ERROR] Se ha producido un error en la ejecución:'||TO_CHAR(SQLCODE)||CHR(10);
      PL_OUTPUT := PL_OUTPUT ||'-----------------------------------------------------------'||CHR(10);
      PL_OUTPUT := PL_OUTPUT ||SQLERRM||CHR(10);
      PL_OUTPUT := PL_OUTPUT ||V_SQL||CHR(10);
      DBMS_OUTPUT.PUT_LINE(PL_OUTPUT);
      ROLLBACK;
      RAISE;
END;
/
EXIT;
