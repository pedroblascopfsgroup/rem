
/*-- BLOQUE QUE COMPRUEBA EXISTENCIA DE ERROR DE COMPILACIÓN EN EL PROCEDURE ANTERIOR --*/
WHENEVER SQLERROR EXIT SQL.SQLCODE;
SET SERVEROUTPUT ON
set linesize 2000
SET FEEDBACK OFF
SET VERIFY OFF

DECLARE

  V_SQL VARCHAR2(2000);
  V_NOMBRE_SCRIPT VARCHAR2(200) := '&1';
  V_NOMBRE_OBJETO VARCHAR2(200) := 'XXXXX';
  V_INDEX_TIPO NUMBER;
  V_ESQUEMA VARCHAR2(200) := '&2';
  V_NUM_FILAS NUMBER;

  ERROR_COMPILACION EXCEPTION;
  PRAGMA EXCEPTION_INIT(ERROR_COMPILACION, -20002);

  CURSOR C_ERRORES (P_NOMBRE IN ALL_ERRORS.NAME%TYPE) IS
    SELECT SEQUENCE, LINE, POSITION, TEXT, ATTRIBUTE, MESSAGE_NUMBER 
    FROM ALL_ERRORS WHERE NAME=P_NOMBRE AND OWNER=V_ESQUEMA ORDER BY SEQUENCE;
  V_ERROR C_ERRORES%ROWTYPE;
  V_DESC_ERROR VARCHAR2(4000 CHAR) := '';

BEGIN

  IF (INSTR(V_NOMBRE_SCRIPT, '_SP_') > 0) OR (INSTR(V_NOMBRE_SCRIPT, '_VI_') > 0) OR (INSTR(V_NOMBRE_SCRIPT, '_MV_') > 0) THEN
    IF (INSTR(V_NOMBRE_SCRIPT, '_SP_') > 0) THEN
      V_INDEX_TIPO := INSTR(V_NOMBRE_SCRIPT, '_SP_');
    ELSIF (INSTR(V_NOMBRE_SCRIPT, '_VI_') > 0) THEN
      V_INDEX_TIPO := INSTR(V_NOMBRE_SCRIPT, '_VI_');
    ELSE
      V_INDEX_TIPO := INSTR(V_NOMBRE_SCRIPT, '_MV_');
    END IF;
    V_NOMBRE_OBJETO := SUBSTR(V_NOMBRE_SCRIPT, V_INDEX_TIPO + 4);
  ELSE
    DBMS_OUTPUT.PUT_LINE('--- EL SCRIPT ' || V_NOMBRE_SCRIPT || ' NO SIGUE LAS CONVENCIONES ESTABLECIDAS.');
  END IF;
  
  DBMS_OUTPUT.PUT_LINE('--- COMPROBACIÓN DE ERRORES DE COMPILACIÓN EN EL SCRIPT: ' || V_NOMBRE_SCRIPT || ' - ESQUEMA: ' || V_ESQUEMA || ' - OBJETO: ' || V_NOMBRE_OBJETO);

  V_SQL := 'SELECT COUNT(*) FROM ALL_ERRORS WHERE NAME=''' || V_NOMBRE_OBJETO || ''' AND OWNER=''' || V_ESQUEMA || '''';
  EXECUTE IMMEDIATE V_SQL INTO V_NUM_FILAS;
  IF V_NUM_FILAS > 0 THEN
    OPEN C_ERRORES(V_NOMBRE_OBJETO);
    LOOP
      FETCH C_ERRORES INTO V_ERROR;
      IF C_ERRORES%NOTFOUND THEN
        CLOSE C_ERRORES;
        EXIT;
      END IF;
      V_DESC_ERROR := V_DESC_ERROR || CHR(10) || ' ---' || V_ERROR.SEQUENCE || ': Linea ' || V_ERROR.LINE || ', posicion ' || V_ERROR.POSITION 
        || ': ' || V_ERROR.TEXT || ' - tipo ' || V_ERROR.ATTRIBUTE || ' (' || V_ERROR.MESSAGE_NUMBER || ')';
    END LOOP;
    DBMS_OUTPUT.PUT_LINE('--- RESUMEN DE ERRORES DE COMPILACIÓN (ESQUEMA ' || V_ESQUEMA || '): ' || V_DESC_ERROR);
    RAISE_APPLICATION_ERROR(-20002, V_DESC_ERROR);
  END IF;
  DBMS_OUTPUT.PUT_LINE('--- NO HA HABIDO ERRORES DE COMPILACIÓN EN EL OBJETO: ' || V_NOMBRE_OBJETO || ' (ESQUEMA ' || V_ESQUEMA || ')');

EXCEPTION
     WHEN ERROR_COMPILACION THEN
        ROLLBACK;
        DBMS_OUTPUT.PUT_LINE(SQLERRM);
        RAISE;

     WHEN OTHERS THEN
        DBMS_OUTPUT.put_line('[ERROR] Se ha producido un error en la ejecución: '||TO_CHAR(SQLCODE));
        DBMS_OUTPUT.put_line('-----------------------------------------------------------'); 
        DBMS_OUTPUT.put_line(SQLERRM);
        
        ROLLBACK;
        RAISE;

END;
/

exit