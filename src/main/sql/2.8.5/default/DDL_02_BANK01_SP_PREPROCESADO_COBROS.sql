whenever sqlerror exit sql.sqlcode;

DECLARE
  N NUMBER(16);
BEGIN
    select count(1) into N from ALL_TAB_COLS where owner='BANK01' and table_name = 'CPR_COBROS_PAGOS_RECOBRO' and column_name = 'FECHA_INI_IRREGU';
    IF (N = 0) THEN
      EXECUTE IMMEDIATE 'ALTER TABLE BANK01.CPR_COBROS_PAGOS_RECOBRO ADD FECHA_INI_IRREGU DATE';
    END IF;
    
    select count(1) into N from ALL_TAB_COLS where owner='BANK01' and table_name = 'CPR_COBROS_PAGOS_RECOBRO' and column_name = 'FECHA_POS_VENCIDA';
    IF (N = 0) THEN
      EXECUTE IMMEDIATE 'ALTER TABLE BANK01.CPR_COBROS_PAGOS_RECOBRO ADD FECHA_POS_VENCIDA DATE';
    END IF;
  
    SELECT COUNT(1) INTO N FROM ALL_INDEXES WHERE INDEX_NAME = 'IDX_CRP_CPR_ID';
    IF (N = 0) THEN
      EXECUTE IMMEDIATE 'CREATE INDEX BANK01.IDX_CRP_CPR_ID ON BANK01.CPR_COBROS_PAGOS_RECOBRO(CPR_ID)';
    END IF;
   
    SELECT COUNT(1) INTO N FROM ALL_INDEXES WHERE INDEX_NAME = 'IDX_CRP_CPA_ID';
    IF (N = 0) THEN
      EXECUTE IMMEDIATE 'CREATE INDEX BANK01.IDX_CRP_CPA_ID ON BANK01.CPR_COBROS_PAGOS_RECOBRO(CPA_ID)';
    END IF;
   
END;
/


CREATE OR REPLACE PROCEDURE PREPROCESADO_COBROS_FACT AS 
  V_LAST_BATCH_EXECUTION DATE;
  V_LAST_DATE_PREPROCESS DATE;
BEGIN
  V_LAST_BATCH_EXECUTION := SYSDATE-1;

  select trunc(max(CCR_FECHA)) into V_LAST_DATE_PREPROCESS from bank01.CCR_CONTROL_COBROS_RECOBRO;
  if (V_LAST_DATE_PREPROCESS IS null) then
    SELECT SYSDATE-1 INTO V_LAST_DATE_PREPROCESS FROM DUAL;
  END IF;
  DBMS_OUTPUT.PUT_LINE('Ultima fecha preprocesado Cobros: '||V_LAST_DATE_PREPROCESS);

  V_LAST_DATE_PREPROCESS := V_LAST_DATE_PREPROCESS + 1;
  WHILE (V_LAST_DATE_PREPROCESS <= V_LAST_BATCH_EXECUTION)
  LOOP
    DBMS_OUTPUT.PUT_LINE('Procesando cobros del dia: '|| V_LAST_DATE_PREPROCESS);
    
    INSERT INTO bank01.CPR_COBROS_PAGOS_RECOBRO
      (CPR_ID, CPA_ID, CPA_FECHA, CNT_ID, EXP_ID, RCF_AGE_ID, RCF_SCA_ID, FECHA_INI_IRREGU, FECHA_POS_VENCIDA)    
    WITH DIAS AS (
      SELECT /*+ MATERIALIZE */ DISTINCT TRUNC(FECHA_HIST) FECHA_HIST FROM bank01.H_REC_FICHERO_CONTRATOS WHERE TRUNC(FECHA_HIST) <> TO_DATE('14/11/2014','dd/MM/yyyy'))
    ,CPA_FECHA_HIST AS (
      SELECT /*+ MATERIALIZE */ CPA.CPA_ID, MAX(TRUNC(D.FECHA_HIST)) FECHA_REPARTO
      FROM bank01.CPA_COBROS_PAGOS CPA
        JOIN DIAS D ON TRUNC(CPA.CPA_FECHA_MOVIMIENTO) >= TRUNC(D.FECHA_HIST)
      WHERE TRUNC(CPA.CPA_FECHA_EXTRACCION) = TRUNC(V_LAST_DATE_PREPROCESS)
      GROUP BY CPA.CPA_ID)
    SELECT bank01.S_CPR_COBROS_PAGOS_RECOBRO.NEXTVAL CPR_ID,
      CPA.CPA_ID, CPA.CPA_FECHA_MOVIMIENTO, CPA.CNT_ID, CRE.EXP_ID, HREC.RCF_AGE_ID, CRE.RCF_SCA_ID,
      HREC.FECHA_INI_IRREGU, HREC.FECHA_POS_VENCIDA
    FROM bank01.CPA_COBROS_PAGOS CPA
      INNER JOIN CPA_FECHA_HIST ON CPA.CPA_ID = CPA_FECHA_HIST.CPA_ID
      INNER JOIN bank01.H_REC_FICHERO_CONTRATOS HREC ON CPA.CNT_ID = SUBSTR(HREC.ID_ENVIO,9)
          AND TRUNC(HREC.FECHA_HIST) =  TRUNC(CPA_FECHA_HIST.FECHA_REPARTO)
      INNER JOIN bank01.CRC_CICLO_RECOBRO_CNT CRC ON CPA.CNT_ID = CRC.CNT_ID
        AND HREC.ID_ENVIO = CRC.CRC_ID_ENVIO
        AND TRUNC(CPA.CPA_FECHA_MOVIMIENTO) BETWEEN TRUNC(CRC.CRC_FECHA_ALTA) AND (TRUNC(NVL(CRC.CRC_FECHA_BAJA,SYSDATE))-1)
        AND CRC.BORRADO = 0
      INNER JOIN bank01.CRE_CICLO_RECOBRO_EXP CRE ON CRC.CRE_ID = CRE.CRE_ID
        AND CRE.RCF_SCA_ID = HREC.RCF_SCA_ID AND CRE.RCF_AGE_ID = HREC.RCF_AGE_ID
        AND CRE.BORRADO = 0
    WHERE CPA.BORRADO = 0 AND CPA.CPA_COBRO_FACTURABLE = 1 AND TRUNC(CPA.CPA_FECHA_EXTRACCION) = V_LAST_DATE_PREPROCESS;

    INSERT INTO bank01.CCR_CONTROL_COBROS_RECOBRO (CCR_ID, CCR_FECHA)
    VALUES (bank01.S_CCR_CONTROL_COBROS_RECOBRO.NEXTVAL, V_LAST_DATE_PREPROCESS);

    V_LAST_DATE_PREPROCESS:=V_LAST_DATE_PREPROCESS + 1;    
    
    COMMIT;
  END LOOP;
EXCEPTION
  WHEN OTHERS THEN
    DBMS_OUTPUT.PUT_LINE('ERROR: '||TO_CHAR(SQLCODE));
    DBMS_OUTPUT.PUT_LINE(SQLERRM);
    
    ROLLBACK;
    RAISE;  
END PREPROCESADO_COBROS_FACT;
/

EXIT